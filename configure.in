dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/RNumber.C)

RNUMBER_MAJOR_VERSION=1
RNUMBER_MINOR_VERSION=23
RNUMBER_MICRO_VERSION=2
RNUMBER_INTERFACE_AGE=0
RNUMBER_BINARY_AGE=0
RNUMBER_VERSION=$RNUMBER_MAJOR_VERSION.$RNUMBER_MINOR_VERSION.$RNUMBER_MICRO_VERSION

AC_SUBST(RNUMBER_MAJOR_VERSION)
AC_SUBST(RNUMBER_MINOR_VERSION)
AC_SUBST(RNUMBER_MICRO_VERSION)
AC_SUBST(RNUMBER_INTERFACE_AGE)
AC_SUBST(RNUMBER_BINARY_AGE)
AC_SUBST(RNUMBER_VERSION)

VERSION=$RNUMBER_VERSION
PACKAGE=rnumber

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AM_CPP_SETUP

#########################################################################
#  Configure arguments 
#########################################################################

web_stuff=0
WEB_HOME=./web-home
AC_ARG_ENABLE(web-home,
[  --enable-web-home=<prefix>    Prefix directory to install web-stuff. (examples, html documentation, etc...)
                          If not specified, web materials are not installed.],
 if test x$enableval = x; then
    WEB_HOME=""
    web_stuff=false
 else
    WEB_HOME=$enableval
    web_stuff=true;
 fi
 AC_MSG_RESULT("Web home is $WEB_HOME")
)

AM_CONDITIONAL(WEB_STUFF, test x$web_stuff = xtrue)
AC_SUBST(WEB_HOME)

dnl On FreeBSD 3.0 (and perhaps some other systems) GNU m4 is
dnl called `gm4' where `m4' is the system's own m4.
AC_CHECK_PROGS(M4, gm4 m4, m4)

dnl Check for the desktop calculator program.
AC_PATH_PROG(DCPROG,dc)
if test "x$DCPROG" = x ; then
        AC_ERROR("Could not find program DC- this is required for the regression tests.")
fi

dnl Check for the location of Perl
AC_PATH_PROG(PERLPROG,perl)
if test "x$PERLPROG" = x ; then
        AC_ERROR("Could not find perl.  Check your path to make sure that it is present.")
fi

dnl find bigloo
AC_ARG_ENABLE(bigloo-bindings,
[  --disable-bigloo-bindings    Enable the building of Bigloo bindings.],
 BIGLOOBINDINGS=0,
 BIGLOOBINDINGS=1
)
if test "x$BIGLOOBINDINGS" != x ; then
	AC_PATH_PROG(BIGLOOPROG,bigloo)
	if test "x$BIGLOOPROG" = x ; then
   	AC_WARN("Could not find bigloo. Check your path to make sure it is present.")
	   AC_WARN("Bigloo is available from http://www-sop.inria.fr/mimosa/fp/Bigloo/.")
   	AC_WARN("Bigloo bindings will *not* be built.")
	   BIGLOO=
	else
   	   BIGLOO=bigloo
	   AC_MSG_RESULT([Bigloo Program:  $BIGLOOPROG])
   	   BIGLOO_BINDIR=`dirname $BIGLOOPROG`
           BIGLOO_REVISION=`bigloo -revision`
	   BIGLOO_MAJOR=`echo $BIGLOO_REVISION | \
	              sed 's/\([[0-9]]*\).\([[0-9]]*\)\([[a-zA-Z]]*\)/\1/'`
	   BIGLOO_MINOR=`echo $BIGLOO_REVISION | \
	              sed 's/\([[0-9]]*\).\([[0-9]]*\)\([[a-zA-Z]]*\)/\2/'`
	   BIGLOO_MICRO=`echo $BIGLOO_REVISION | \
	              sed 's/\([[0-9]]*\).\([[0-9]]*\)\([[a-zA-Z]]*\)/\3/'`
           AC_MSG_RESULT("bigloo major $BIGLOO_MAJOR minor $BIGLOO_MINOR micro $BIGLOO_MICRO")
	   if test $BIGLOO_MAJOR -ge 2 ;then 
	     if test $BIGLOO_MINOR -ge 6; then
	     	   BIGLOO_NEW_ENOUGH=yes;
             else
		   BIGLOO=
                   BIGLOO_NEW_ENOUGH=no;
             fi
           else
   		   BIGLOO=;
		   BIGLOO_NEW_ENOUGH=no; 
           fi
           AC_MSG_RESULT("bigloo new enough (need 2.6a or newer): $BIGLOO_NEW_ENOUGH")
   	   BIGLOO_LIBDIR=${BIGLOO_BINDIR}/../lib/bigloo/${BIGLOO_REVISION}
	   AC_MSG_RESULT([Bigloo bin dir:  $BIGLOO_BINDIR BIGLOO_LIBDIR $BIGLOO_LIBDIR])
   	   AC_SUBST(BIGLOO_LIBDIR)
	   if test $GCC_MAJOR -ge 3; then
              BIGLOO_GCC3X="-lgcc_s"
	   else
              BIGLOO_GCC3X=""
           fi
	   AC_MSG_RESULT([BIGLOO_GCC3X $BIGLOO_GCC3X])
	   AC_SUBST(BIGLOO_GCC3X)
	fi
else
	echo "The building of Bigloo bindings has been disabled."
fi

AC_SUBST([BIGLOO])
AM_CONDITIONAL(WITHOUT_BIGLOO,test x$BIGLOO = x)

if test x$BIGLOO = x; then
   AC_MSG_WARN([Bigloo bindings will not be built.])
fi

dnl find swig
AC_ARG_ENABLE(perl-bindings,
[  --disable-perl-bindings    Enable the building of Perl bindings.],
 PERLBINDINGS=,
 PERLBINDINGS=1
)
if test "x$PERLBINDINGS" != x ; then
	AC_PATH_PROG(SWIGPROG,swig)
	if test "x$SWIGPROG" = x ; then
   	AC_WARN("Could not find swig. Check your path to make sure it is present.")
	   AC_WARN("Swig is available from http://www.swig.org/.")
   	AC_WARN("Since swig is needed for the perl bindings\, the perl bindings will *not* be built.")
	   PERLBINDINGS=
	else
           PERLBINDINGS=perl
	   PERL_ARCH_INCL="-I `perl -e 'use Config;print $Config{archlib};'`/CORE"
	   PERL_CC_FLAGS="-fPIC `perl -e 'use Config; print $Config{ccflags}'`"
           AC_MSG_RESULT([SWIG: $SWIGPROG])
           AC_MSG_RESULT([SWIG: $PERL_ARCH_INCL])
           AC_MSG_RESULT([SWIG: $PERL_CC_FLAGS])
	fi
else
	echo "The building of Perl bindings has been disabled."
fi
AC_SUBST(PERL_ARCH_INCL)
AC_SUBST(PERL_CC_FLAGS)
AC_SUBST([PERLBINDINGS])
AM_CONDITIONAL(WITHOUT_SWIG,test x$PERLBINDINGS = x)

dnl find scribe. If scribe is not available, don't build the documentation
AC_PATH_PROG(SCRIBEPROG,scribe)
if test "x$SCRIBEPROG" = x ; then
   AC_WARN("Could not find scribe. Check your path to make sure it is present.")
   AC_WARN("Scribe is available from http://www-sop.inria.fr/mimosa/Manuel.Serrano/scribe/")
   AC_WARN("Since scribe is needed for the documentation\, the documentation will *not* be built.")
   DOC=
else
   DOC=doc
   AC_MSG_RESULT("SCRIBE $SCRIBEPROG")
fi
AC_SUBST([DOC])
AM_CONDITIONAL(WITHOUT_DOC,test x$DOC = x)


dnl C++ compiler feature check start here
dnl moved the c++ compiler alittle to make the basename check work right..
AC_LANG_CPLUSPLUS

CXXFLAGS="-Wall -g $CPPSETUP"

AC_SUBST(PROFILE)
AC_SUBST(ACLOCAL_FLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(EXPLTEMPLATES)
AC_PROG_INSTALL
AC_STDC_HEADERS

AC_CONFIG_FILES([\
   Makefile \
   doc/Makefile \
   doc/user/Makefile \
   macros/Makefile \
   include/Makefile \
   src/rnumber_feature.h \
   src/Makefile \
   bigloo/Makefile \
   bigloo/src/Makefile \
   bigloo/src/rnumber-version.h \
   bigloo/src/rnumber/Makefile \
   bigloo/src/bl-rnumber.init \
   bigloo/tests/Makefile \
   perl/Makefile \
   perl/src/Makefile \
   perl/tests/Makefile \
   tests/Makefile])

AC_CONFIG_FILES([rnumber-config],[chmod +x rnumber-config])

AC_OUTPUT
